---
subcategory: "SES (Simple Email)"
layout: "aws"
page_title: "AWS: aws_ses_domain_dkim"
description: |-
  Provides an SES domain DKIM generation resource
---

# Resource: aws_ses_domain_dkim

Provides an SES domain DKIM generation resource.

Domain ownership needs to be confirmed first using ses_domain_identity Resource

## Argument Reference

The following arguments are supported:

* `domain` - (Required) Verified domain name to generate DKIM tokens for.

## Attributes Reference

In addition to all arguments above, the following attributes are exported:

* `dkim_tokens` - DKIM tokens generated by SES.
  These tokens should be used to create CNAME records used to verify SES Easy DKIM.
  See below for an example of how this might be achieved
  when the domain is hosted in Route 53 and managed by this provider.
  Find out more about verifying domains in Amazon SES
  in the [AWS SES docs](http://docs.aws.amazon.com/ses/latest/DeveloperGuide/easy-dkim-dns-records.html).

## Example Usage

```terraform
resource "aws_ses_domain_identity" "example" {
  domain = "example.com"
}

resource "aws_ses_domain_dkim" "example" {
  domain = aws_ses_domain_identity.example.domain
}

resource "aws_route53_record" "example_amazonses_dkim_record" {
  count   = 3
  zone_id = "ABCDEFGHIJ123"
  name    = "${aws_ses_domain_dkim.example.dkim_tokens[count.index]}._domainkey"
  type    = "CNAME"
  ttl     = "600"
  records = ["${aws_ses_domain_dkim.example.dkim_tokens[count.index]}.dkim.amazonses.com"]
}
```

## Import

DKIM tokens can be imported using the `domain` attribute, e.g.,

```
$ terraform import aws_ses_domain_dkim.example example.com
```
